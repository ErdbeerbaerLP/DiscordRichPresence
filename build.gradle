buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.3.3"
group = "de.erdbeerbaerlp.discordrpc" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "discordrpc"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.4.2705"
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}
repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}
configurations {
    shade
    compile.extendsFrom shade
}
configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}
dependencies {
    shade('club.minnced:java-discord-rpc:2.0.2') {
        exclude module: "jna"
        exclude module: "gson"
    }
    deobfCompile('com.github.ErdbeerbaerLP:ErdbeerbaerGuiLib:master-SNAPSHOT') {
        changing = true
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
jar {
    classifier = "1.12"
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    manifest {
        attributes([
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'     : System.currentTimeMillis()
        ])
    }
}
curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    if (project.hasProperty('curseforge.apikey')) {
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '300569'
            changelog = project.changelog
            releaseType = 'beta'
            addGameVersion '1.12'
            addGameVersion '1.12.1'
            addGameVersion '1.12.2'
            mainArtifact(jar) {
                displayName = "DiscordRichPresence-$version (MC 1.12)"
            }
            relations {
                optionalDependency 'ftb-utilities'
                optionalDependency 'emojicord'
                optionalDependency 'voting'
            }
        }
    }
}
tasks.curseforge.dependsOn(build)