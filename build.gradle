buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = '2.0.7'
group = 'de.erdbeerbaerlp.discordrpc' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'discordrpc'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
configurations {
    shade
    compile.extendsFrom(shade)
}
shadowJar {
    classifier = '1.16.4'
    configurations = [project.configurations.shade]
    exclude 'com/sun/jna/**'
    relocate 'com.google', 'drpcshadow.com.google'
    relocate 'org.apache.http.entity.mime', 'somesubpackage.org.apache.http.entity.mime'
}

minecraft {
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20200723-1.16.1'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    //accessTransformer = file('my_at.cfg')

    // default run configurations.
    // these can be tweaked, removed, or duplicated as needed.
    runs {
        client = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'info'
            workingDirectory project.file('run').canonicalPath
            mods {
                discordrpc {
                    source sourceSets.main
                }
            }
        }
        server = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'info'
            workingDirectory project.file('run').canonicalPath
            mods {
                discordrpc {
                    source sourceSets.main
                }
            }
        }
    }
}
repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    flatDir {
        dirs 'libs'
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
	//Forge
    minecraft 'net.minecraftforge:forge:1.17.1-37.0.84'
    //Hive MC Libs
    shade 'com.github.NukerHD:hive-java-api:1.0-snapshot'
    compile 'com.github.NukerHD:hive-java-api:1.0-snapshot'
    //DiscordRichPresence lib
    shade "com.google.guava:guava:30.1-jre"
    shade 'com.github.Vatuu:discord-rpc:1.6.2'
    shade 'com.github.Vatuu:discord-rpc-binaries:3.4.0'

    //Gui Library
    compile 'eguilib:eguilib:1.0.5:1.16'
}
reobf {
    shadowJar  {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
artifacts {
    archives tasks.shadowJar
}
// Example for how to get properties into the manifest for reading by the runtime..
jar {
    classifier = "1.16.4"
    manifest {
        attributes(["Specification-Title": "discordrpc",
                    "Specification-Vendor": "discordrpc",
                    "Specification-Version": "2.0", 
                    "Implementation-Title": project.name,
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"discordrpc",
                    'Target-Platforms': 'win32-x86-64, win32-x86, linux-x86-64, darwin',
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}
curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    if (project.hasProperty('curseforge.apikey')) {
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '300569'
            changelog = project.changelog
            releaseType = 'release'
            addGameVersion '1.16.4'
            addGameVersion '1.16.5'
            mainArtifact(jar) {
                displayName = "DiscordRichPresence-$version (MC 1.16.4+)"
            }
            relations {
                requiredDependency 'eguilib'
            }
        }
    }
}
tasks.curseforge.dependsOn(build)